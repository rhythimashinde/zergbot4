use Manager as knowledge.
order=linearall.

module ManagerEvent {
	%%%%%%%%%%%%%%% --------------------------------------- Messaging --------------------------------------- %%%%%%%%%%%%%%%
	
	% If enough zerglings gathered, tell zerglings to attack
	forall bel(allReady) do allother.send(attack).
	
	% If a drone has died, delete him from knowledge base
	forall (_).sent:(death(Id)), bel(drone(Name, Id)) do delete(drone(Name, Id)).
	
	% If drone info received, insert it in knowledge base
	forall (Name).sent(drone(Id)) do insert(drone(Name, Id)).
	
	% If zergling is ready, insert it in knowledge base
	forall (_).sent:(ready(Id)) do insert(ready(Id)).	
	
	% When hatcheryPosition received, insert it to knowledge base
	if (_).sent:(hatcheryPosition(X, Y)) then insert(hatcheryPosition(X, Y)).
	
	% If believe done, but the corresponding drone does not exist anymore, delete done from knowledge base
	if bel(done(Name, Id), not(drone(Name, Id))) then delete(done(Name, Id)).
	
	% If not believing done yet and have info about drone, insert in knowledge base and tell the drone he is the builder 
	if bel(not(done(Name, Id)), drone(Name, Id)) then insert(done(Name, Id)) + (Name).send(builder).
	
	% If hatchery position is requested, send the nexus location
	forall (Name).sent?(hatcheryPosition), bel(hatcheryPosition(X, Y)) do (Name).send:(hatcheryPosition(X, Y)).
}